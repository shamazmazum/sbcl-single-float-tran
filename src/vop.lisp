(in-package :sb-vm)

(macrolet ((frob (op-name vop-name op-inst double-p)
             (let ((sc-name   (if double-p 'double-reg 'single-reg))
                   (move-inst (if double-p 'movsd 'movss))
                   (immediate (if double-p 'fp-double-immediate 'fp-single-immediate))
                   (type      (if double-p 'double-float 'single-float)))
               `(define-vop (,vop-name)
                  (:translate ,op-name)
                  (:policy :fast-safe)
                  (:temporary (:sc ,sc-name) tmp)
                  (:note "inline float arithmetic")
                  (:vop-var vop)
                  (:save-p :compute-only)
                  (:args (x :scs (,sc-name ,immediate)
                            :target r
                            :load-if (not (sc-is x ,immediate)))
                         (y :scs (,sc-name ,immediate)
                            :load-if (not (sc-is y ,immediate))))
                  (:results (r :scs (,sc-name)))
                  (:arg-types ,type ,type)
                  (:result-types ,type)
                  (:generator
                   2
                   (flet ((get-constant (tn)
                            (let ((value (tn-value tn)))
                              (register-inline-constant value))))
                     (cond
                       ((location= x r)
                        (note-float-location ',op-name vop x y)
                        (when (sc-is y ,immediate)
                          (setf y (get-constant y)))
                        (inst ,op-inst x y))
                       ((not (location= r y))
                        (if (sc-is x ,immediate)
                            (inst ,move-inst r (get-constant x))
                            (move r x))
                        (note-float-location ',op-name vop r y)
                        (when (sc-is y ,immediate)
                          (setf y (get-constant y)))
                        (inst ,op-inst r y))
                       (t
                        (if (sc-is x ,immediate)
                            (inst ,move-inst tmp (get-constant x))
                            (move tmp x))
                        (note-float-location ',op-name vop tmp y)
                        (inst ,op-inst tmp y)
                        (move r tmp)))))))))
  (frob sbcl-single-float-tran::%minf fminf minss nil)
  (frob sbcl-single-float-tran::%mind fmind minsd t)
  (frob sbcl-single-float-tran::%maxf fmaxf maxss nil)
  (frob sbcl-single-float-tran::%maxd fmaxd maxsd t))
